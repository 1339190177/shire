{
  parserClass="com.phodal.shirelang.parser.ShireParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Shire"
  psiImplClassSuffix="Impl"
  psiPackage="com.phodal.shirelang.psi"
  psiImplPackage="com.phodal.shirelang.psi.impl"

  elementTypeHolderClass="com.phodal.shirelang.psi.ShireTypes"
  elementTypeClass="com.phodal.shirelang.psi.ShireElementType"
  tokenTypeClass="com.phodal.shirelang.lexer.ShireTokenType"

  tokens=[
    AGENT_START        = "AGENT_START"
    COMMAND_START      = "COMMAND_START"
    VARIABLE_START     = "VARIABLE_START"
    SYSTEM_START       = "SYSTEM_START"
    CODE_BLOCK_START   = "CODE_BLOCK_START"
    CODE_BLOCK_END     = "CODE_BLOCK_END"
    CODE_BLOCK         = "CODE_BLOCK"
    CODE_CONTENT       = "CODE_CONTENT"
    IDENTIFIER         = "IDENTIFIER"
    LANGUAGE_ID        = "LANGUAGE_ID"
    VARIABLE_ID        = "VARIABLE_ID"
    COMMAND_ID         = "COMMAND_ID"
    AGENT_ID           = "AGENT_ID"
    SYSTEM_ID          = "SYSTEM_ID"
    COLON              = "COLON"
    COMMAND_PROP       = "COMMAND_PROP"
    SHARP              = "SHARP"
    LINE_INFO          = "LINE_INFO"
    FRONTMATTER_START  = "FRONTMATTER_START"
    FRONTMATTER_END    = "FRONTMATTER_END"
    FRONTMATTER_KEY    = "FRONTMATTER_KEY"
    LBRACKET           = "LBRACKET"
    RBRACKET           = "RBRACKET"
    INDENT             = "INDENT"
    OPEN_BRACE         = "{"
    CLOSE_BRACE       = "}"
    LPAREN             = "("
    RPAREN             = ")"
    CASE               = "case"
  ]
}

ShireFile ::= frontMatterHeader? (used | code | TEXT_SEGMENT | NEWLINE | COMMENTS)*

frontMatterHeader ::= FRONTMATTER_START NEWLINE frontMatterEntries FRONTMATTER_END

frontMatterEntries ::= ((frontMatterEntry | patternAction) WHITE_SPACE?)*
frontMatterEntry ::= frontMatterKey WHITE_SPACE? COLON WHITE_SPACE? (frontMatterValue) NEWLINE?
frontMatterKey ::= FRONTMATTER_KEY | QUOTE_STRING | PATTERN
frontMatterValue ::= STRING | NUMBER | QUOTE_STRING | DATE | PATTERN | BOOLEAN | frontMatterArray | (NEWLINE objectKeyValue)
frontMatterArray ::= LBRACKET (frontMatterValue (COMMA frontMatterValue)*) RBRACKET

objectKeyValue ::= (INDENT keyValue NEWLINE?)*
keyValue ::= frontMatterEntry | patternAction

patternAction ::= PATTERN WHITE_SPACE? matchAction | caseAction (WHITE_SPACE | NEWLINE)*

matchAction ::= NEWLINE? CASE_BLOCK_START (WHITE_SPACE? pipelineFunc WHITE_SPACE? PIPE)* WHITE_SPACE? pipelineFunc WHITE_SPACE? CASE_BLOCK_END

pipelineFunc ::= (funcCall | funcName)
funcCall ::= funcName LPAREN pipelineArgs? RPAREN
funcName ::= IDENTIFIER
pipelineArgs ::= (pipelineArg (COMMA pipelineArg)*)?
pipelineArg ::= STRING | NUMBER | IDENTIFIER | QUOTE_STRING

caseAction ::= "case" WHITE_SPACE PATTERN WHITE_SPACE "in" WHITE_SPACE CASE_BLOCK_START casePatternAction* CASE_BLOCK_END
casePatternAction ::= PATTERN COLON WHITE_SPACE matchAction
CASE_BLOCK_START ::= OPEN_BRACE (WHITE_SPACE | NEWLINE)*
CASE_BLOCK_END ::=(WHITE_SPACE | NEWLINE)* CLOSE_BRACE

used ::= (
    AGENT_START AGENT_ID
    | COMMAND_START COMMAND_ID (COLON COMMAND_PROP (SHARP LINE_INFO)?)?
    | VARIABLE_START VARIABLE_ID
    | SYSTEM_START SYSTEM_ID COLON NUMBER
)

code ::=  CODE_BLOCK_START LANGUAGE_ID? NEWLINE? code_contents? CODE_BLOCK_END?

code_contents ::= (NEWLINE | CODE_CONTENT)*