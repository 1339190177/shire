{
  parserClass="com.phodal.shirelang.parser.ShireParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Shire"
  psiImplClassSuffix="Impl"
  psiPackage="com.phodal.shirelang.psi"
  psiImplPackage="com.phodal.shirelang.psi.impl"

  elementTypeHolderClass="com.phodal.shirelang.psi.ShireTypes"
  elementTypeClass="com.phodal.shirelang.psi.ShireElementType"
  tokenTypeClass="com.phodal.shirelang.lexer.ShireTokenType"

  tokens=[
    AGENT_START        = "AGENT_START"
    COMMAND_START      = "COMMAND_START"
    VARIABLE_START     = "VARIABLE_START"
    SYSTEM_START       = "SYSTEM_START"
    CODE_BLOCK_START   = "CODE_BLOCK_START"
    CODE_BLOCK_END     = "CODE_BLOCK_END"
    CODE_BLOCK         = "CODE_BLOCK"
    CODE_CONTENT       = "CODE_CONTENT"
    IDENTIFIER         = "IDENTIFIER"
    LANGUAGE_ID        = "LANGUAGE_ID"
    VARIABLE_ID        = "VARIABLE_ID"
    COMMAND_ID         = "COMMAND_ID"
    AGENT_ID           = "AGENT_ID"
    SYSTEM_ID          = "SYSTEM_ID"
    COLON              = "COLON"
    COMMAND_PROP       = "COMMAND_PROP"
    SHARP              = "SHARP"
    LINE_INFO          = "LINE_INFO"
    FRONTMATTER_START  = "FRONTMATTER_START"
    FRONTMATTER_END    = "FRONTMATTER_END"
    FRONTMATTER_KEY    = "FRONTMATTER_KEY"
    LBRACKET           = "LBRACKET"
    RBRACKET           = "RBRACKET"
    INDENT             = "INDENT"
    ARROW              = "ARROW"
    OPEN_BRACE         = "{"
    CLOSE_BRACE        = "}"
    LPAREN             = "("
    RPAREN             = ")"
    CASE               = "case"
    DEFAULT            = "default"
    DASH               = "-"
  ]
}

ShireFile ::= frontMatterHeader? (used | code | TEXT_SEGMENT | NEWLINE | COMMENTS)*

frontMatterHeader ::= FRONTMATTER_START NEWLINE frontMatterEntries FRONTMATTER_END

frontMatterEntries ::= ((frontMatterEntry) WHITE_SPACE?)*
frontMatterEntry ::= frontMatterKey WHITE_SPACE? COLON WHITE_SPACE? (frontMatterValue) NEWLINE?
frontMatterKey ::= FRONTMATTER_KEY | QUOTE_STRING | PATTERN
frontMatterValue ::= STRING | NUMBER | QUOTE_STRING | DATE | BOOLEAN | frontMatterArray | (NEWLINE objectKeyValue)
frontMatterArray ::= LBRACKET (frontMatterValue (COMMA frontMatterValue)*) RBRACKET

objectKeyValue ::= (INDENT keyValue NEWLINE?)*
keyValue ::= frontMatterEntry | patternAction

patternAction ::= QUOTE_STRING WHITE_SPACE? COLON WHITE_SPACE? PATTERN WHITE_SPACE? actionBlock
actionBlock ::=  BLOCK_START (actionBody) BLOCK_END
actionBody ::= (actionExpr PIPE)* actionExpr
actionExpr ::= WHITE_SPACE? (caseBody | funcCall) WHITE_SPACE?

funcCall ::= funcName (LPAREN pipelineArgs? RPAREN)?
funcName ::= IDENTIFIER
pipelineArgs ::= (pipelineArg (COMMA pipelineArg)*)?
pipelineArg ::= STRING | NUMBER | IDENTIFIER | QUOTE_STRING

caseBody ::= CASE (WHITE_SPACE | NEWLINE)* QUOTE_STRING BLOCK_START casePatternAction* BLOCK_END
casePatternAction ::= caseCondition BLOCK_START (actionExpr PIPE)* actionExpr BLOCK_END
caseCondition ::= DEFAULT | PATTERN | QUOTE_STRING

BLOCK_START ::= (WHITE_SPACE | NEWLINE)* OPEN_BRACE (WHITE_SPACE | NEWLINE)*
BLOCK_END ::= (WHITE_SPACE | NEWLINE)* CLOSE_BRACE (WHITE_SPACE | NEWLINE)*

used ::= (
    AGENT_START AGENT_ID
    | COMMAND_START COMMAND_ID (COLON COMMAND_PROP (SHARP LINE_INFO)?)?
    | VARIABLE_START VARIABLE_ID
    | SYSTEM_START SYSTEM_ID COLON NUMBER
)

code ::=  CODE_BLOCK_START LANGUAGE_ID? NEWLINE? code_contents? CODE_BLOCK_END?

code_contents ::= (NEWLINE | CODE_CONTENT)*